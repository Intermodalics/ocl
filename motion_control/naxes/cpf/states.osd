StateMachine Default
{

  var bool calibrate_offsets     = true
  var bool move_to               = true

  const double pi = 3.14159265358979

  initial state StartRobotState {
    entry {
      do Kuka160.prepareForUse()
    }
    exit {
      do Kuka160.unlockAllAxes()
      do Kuka160.startAllAxes()
    }
    transitions {
      select CalibrateOffsetsState
    }
  }



  state CalibrateOffsetsState {
    preconditions {
      if (calibrate_offsets == false) then
        select MoveToState
    }
    entry {
      do nAxesSensorPos.start()
      do nAxesGeneratorPos.start()
      do nAxesControllerPos.start()
      do nAxesEffectorVel.start()
      do programs.CalibrateOffsetsProg.start()
    }
    exit {
      do nAxesSensorPos.stop()
      do nAxesGeneratorPos.stop()
      do nAxesControllerPos.stop()
      do nAxesEffectorVel.stop()
    }
    transitions {
      if !programs.CalibrateOffsetsProg.isRunning then
       select MoveToState
    }
  }      

  state MoveToState {
    preconditions {
      if (move_to == false) then
        select StopRobotState
    }
    entry {
      do MoveToJoint(0.0, 0.0, 0.0, 0.0, 0.0, 0.0)
    }
    exit {
    }
    transitions {
      select StopRobotState
    }
  }


  final state StopRobotState {
    entry {
        do Kuka160.stopAllAxes()
        do Kuka160.lockAllAxes()
    }
    exit {
      do Kuka160.prepareForShutdown()
    }
  }
}

RootMachine Default Default
